/// @title Escrow
/// @notice SPDX-License-Identifier: MIT
/// @author floop <https://github.com/0xFloop>
/// @notice Adapted from OpenZeppelin (https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/escrow/Escrow.sol)

#include "huffmate/auth/Owned.huff"
#include "huffmate/data-structures/Hashmap.huff"

// Additional Functions
#define function withdraw(address payable) nonpayable returns ()
#define function deposit(address) payable returns ()
#define function depositsOf(address) view returns (uint256)

//Events
#define event Deposited(address indexed payee, uint256 weiAmount)
#define event Withdrawn(address indexed payee, uint256 weiAmount)

#define constant DEPOSITED_EVENT_SIG = 0x2da466a7b24304f47e87fa2e1e5a81b9831ce54fec19055ce277ca2f39ba42c4
#define constant WITHDRAWN_EVENT_SIG = 0x7084f5476618d8e60b11ef0d7d3f06914655adb8793e28ff7f018d4c76d505d5

//Deposits mapping slot
#define constant DEPOSITS_SLOT = FREE_STORAGE_POINTER()

//Constructor
#define macro CONSTRUCTOR() = takes (0) returns (0) {
    OWNED_CONSTRUCTOR()
}


/**
* @dev Withdraw accumulated balance for a payee, forwarding all gas to the
* recipient.
*
* WARNING: Forwarding all gas opens the door to reentrancy vulnerabilities.
* Make sure you trust the recipient, or are either following the
* checks-effects-interactions pattern or using {ReentrancyGuard}.
*
* @param payee The address whose funds will be withdrawn and transferred to.
*
* Emits a {Withdrawn} event.
*/
#define macro WITHDRAW() = takes(0) returns(0) {
    IS_OWNER()

    0x04 calldataload             //[payee]

    dup1                          //[payee, payee]

    dup1                          //[payee, payee]

    [DEPOSITS_SLOT]               // [DEPOSITS_SLOT, payee, payee, payee]

    LOAD_ELEMENT_FROM_KEYS(0x00)  // [payee deposits, payee, payee]

    dup1                          // [payee deposits, payee deposits, payee, payee]

    swap2                         // [payee, payee deposits, payee deposits, payee]

    0x00                          // [0, payee, payee deposits, payee deposits, payee]

    swap1                         // [payee, 0, payee deposits, payee deposits, payee]

    [DEPOSITS_SLOT]               // [DEPOSITS_SLOT, payee, 0, payee deposits, payee deposits, payee]

    STORE_ELEMENT_FROM_KEYS(0x00) // [payee deposits, payee deposits, payee]


    // send the deposits amount to caller
    
    0x00                           // [retOffset, value, payee deposits, payee]
    0x00                           // [argSize, retOffset, value, payee deposits, payee]
    0x00                           // [argOffset, argSize, retOffset, value, payee deposits, payee]
    0x00                           // [retSize, argOffset, argSize, retOffset, value, payee deposits, payee]
    swap4                          // [value, argOffset, argSize, retOffset, retSize, payee deposits, payee]
    0x04 calldataload              // [to, value, argOffset, argSize, retOffset, retSize, payee deposits, payee]
    gas                            // [gas, to, value, argOffset, argSize, retOffset, retSize, payee deposits, payee]

    call                           // [payee deposits, payee]

    swap1                          // [payee, payee deposits]

    [WITHDRAWN_EVENT_SIG]          // [sig, payee, payee deposits]
    0x00 0x00                      // [0, 0, sig, payee, payee deposits]
    log3    

    stop
}

/// @notice Deposit
/// @notice Deposits msg.value to the payee address
#define macro DEPOSIT() = takes(0) returns(0) {
    IS_OWNER()

    callvalue                     // [msg.value]

    0x04 calldataload             // [payee, msg.value]

    dup2                          // [msg.value, payee, msg.value]

    dup1                          // [payee, msg.value, payee, msg.value]

    [DEPOSITS_SLOT]               // [DEPOSITS_SLOT, payee, msg.value, payee, msg.value]
    
    LOAD_ELEMENT_FROM_KEYS(0x00)  // [payee deposits, msg.value, payee, msg.value]

    add                           // [payee deposits + msg.value, payee, msg.value]

    0x04 calldataload             //[payee, payee deposits + msg.value, payee, msg.value]

    [DEPOSITS_SLOT]               //[DEPOSITS_SLOT, payee, payee deposits + msg.value, payee, msg.value]

    STORE_ELEMENT_FROM_KEYS(0x00) // [payee, msg.value]

    [DEPOSITED_EVENT_SIG]         // [sig, payee, msg.value]
    0x00 0x00                     // [0, 0, sig, payee, msg.value]
    log3    
    
    stop
}

#define macro DEPOSITS_OF() = takes(0) returns(0) {
    0x04 calldataload             //[payee]

    [DEPOSITS_SLOT]               // [DEPOSITS_SLOT, payee]
    
    LOAD_ELEMENT_FROM_KEYS(0x00)  // [payee deposits]

    0x00 mstore                   // []
    0x20 0x00 return 
}

#define macro MAIN() = takes (0) returns (0) {
    // Load the function selector

    pc                            // [0x00]
    calldataload                  // [sigWith28ExtraBytes]
    0xE0                          // [0xE0, sigWith28ExtraBytes]
    shr                           // [sig]

    OWNED_MAIN()    

    dup1                          // [sig, sig]
    __FUNC_SIG(withdraw)          // [withdrawSig, sig, sig]
    eq                            // [(sig == withdrawSig), sig]
    withdraw                      // [withdraw, (sig == withdrawSig), sig]
    jumpi                         // [sig]


    dup1                          // [sig, sig]
    __FUNC_SIG(deposit)           // [depositSig, sig, sig]
    eq                            // [(sig == depositSig), sig]
    deposit                       // [deposit, (sig == depositSig), sig]
    jumpi                         // [sig]


    dup1                          // [sig, sig]
    __FUNC_SIG(depositsOf)        // [depositsOfSig, sig, sig]
    eq                            // [(sig == depositSig), sig]
    depositsOf                    // [depositsOf, (sig == depositsOfSig), sig]
    jumpi                         // [sig]

    
    0x00                          //[0x00, sig]
    dup1                          //[0x00, 0x00, sig]
    revert                        //[]

    withdraw:
        WITHDRAW()
    deposit:
        DEPOSIT()
    depositsOf:
        DEPOSITS_OF()
}
